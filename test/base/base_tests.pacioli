import testing;
import si;

declare base_suites :: List(TestSuite);

define base_suites = [
   indexing_suite,
   comprehensions_suite,
   sort_suite,
   reverse_suite,
   nth_suite,
   top_suite,
   bottom_suite,
   total_suite,
   #ranking_suite,   does not exist in MVM yet
   min_suite,
   max_suite,
   tan_suite,
   expt_suite,
   mexpt_suite,
   random_suite
   #mapnz_suite       does not exist in MVM yet
   #kronecker_suite   TODO port in pacioli-ts
];

run_test_suites(base_suites); 

# ------------------------------------------------------------------------------
# Test vectors
# ------------------------------------------------------------------------------

defindex Bar = {v0, v1, v2, v3, v4, v5};

defunit Bar!unit = {
    v0: second,
    v4: metre,
    v5: gram/second^2
};

define test_vec(v0, v1, v2, v3, v4, v5) =
   v0 '.*' delta(Bar@v0) +
   v1 '.*' delta(Bar@v1) +
   v2 '.*' delta(Bar@v2) +
   v3 '.*' delta(Bar@v3) +
   v4 '.*' delta(Bar@v4) +
   v5 '.*' delta(Bar@v5);

defindex Baz = {v0, v1};

defunit Baz!unit = {
    v0: second,
    v1: metre
};

define test_vec2(v0, v1) =
   v0 '.*' delta(Baz@v0) +
   v1 '.*' delta(Baz@v1);

# ------------------------------------------------------------------------------
# Indexing
# ------------------------------------------------------------------------------

define indexing_suite = test_suite("Indexing", [
    
   test("should work for scalars with _", () ->
      get(42, _, _) = 42
   ),
      
   test("should work for vectors with _", () ->
      get(test_vec(1,2,3,4,5,6), Bar@v3, _) = 4
   ),
      
   test("should work for transposed vectors with _", () ->
      get(test_vec(1,2,3,4,5,6)^T, _, Bar@v3) = 4
   )
]);

# ------------------------------------------------------------------------------
# Comprehensions
# ------------------------------------------------------------------------------

define comprehensions_suite = test_suite("Comprehension", [
    
    test("should count empty list as zero", () ->
        count[true | x <- []] = 0
    ),

    test("should count true as one", () ->
        count[true | x <- [1, 2, 3]] = 3
    ),

    test("should count false as zero", () ->
        count[false | x <- [1, 2, 3]] = 0
    ),
            
    test("count comprehension should filter correctly I", () ->
        count[mod(x, 2) = 0 | x <- naturals(10)] = 5
    ),
         
    test("count comprehension should filter correctly II", () ->
        count[ x < y | x <- naturals(10), y <- naturals(10)] = 45
    ),
             
    test("all comprehension should be true for empty list", () ->
        all[x | x <- []] = true
    ),
         
    test("some comprehension should be false for empty list", () ->
        some[x | x <- []] = false
    ),
             
    test("all comprehension should be true for non-empty list", () ->
        all[x < 5 | x <- [1,2,3]] = true
    ),
         
    test("some comprehension should be true for non-empty list", () ->
        some[x < 5 | x <- [1,2,3]] = true
    ),
             
    test("all comprehension should be false for non-empty list", () ->
        all[x < 3 | x <- [1,2,3]] = false
    ),
         
    test("some comprehension should be true for non-empty list", () ->
        some[x < 3 | x <- [1,2,3]] = true
    )
]);

# ------------------------------------------------------------------------------
# Function sort
# ------------------------------------------------------------------------------

define sort_suite = test_suite("Function sort", [
    
    test("should sort ascending",
         () ->
            sort_list([1, 9, 3, 2, 5], less) = [1, 2, 3, 5, 9]
         ),
         
    test("should sort decending",
         () ->
             sort_list([1, 9, 3, 2, 5], greater) = [9, 5, 3, 2, 1]
         )
    ]);

# ------------------------------------------------------------------------------
# Function reverse
# ------------------------------------------------------------------------------

define reverse_suite = test_suite("Function reverse", [
    
    test("should reverse the empty list",
         () ->
            reverse([]) = []
         ),

    test("should reverse a list of numbers correctly",
         () ->
            reverse([1, 9, 3, 2, 5]) = [5, 2, 3, 9, 1]
         )
   ]);

# ------------------------------------------------------------------------------
# Function nth
# ------------------------------------------------------------------------------

define nth_suite = test_suite("Function nth", [
    
    test("should get 0-th element",
         () ->
            nth(0, [1, 9, 3, 2, 5]) = 1
         ),

    test("should get last element",
         () ->
            nth(4, [1, 9, 3, 2, 5]) = 5
         )
   ]);

# ------------------------------------------------------------------------------
# Function top
# ------------------------------------------------------------------------------

define top_suite = test_suite("Function top", [
    
    test("should do nothing for a zero vector",
         () ->
            top(3, test_vec(0, 0, 0, 0, 0, 0)) = test_vec(0, 0, 0, 0, 0, 0)
         ),
   
    test("should do nothing for a vector with one entry",
         () ->
            top(3, test_vec(0, 0, 1, 0, 0, 0)) = test_vec(0, 0, 1, 0, 0, 0)
         ),
   
    test("should be zero for a vector with one entry and top 0",
         () ->
            top(0, test_vec(0, 0, 1, 0, 0, 0)) = test_vec(0, 0, 0, 0, 0, 0)
         ),
   
    test("should get top 2 a vector with more entries",
         () ->
            top(2, test_vec(0, 2, 1, 4, 6, 5)) = test_vec(0, 0, 0, 0, 6, 5)
         ),
   
    test("should get top 2 a vector with entries with ties",
         () ->
            top(2, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 0, 0, 5, 6, 0)
         ),
   
    test("should get top 3 a vector with entries with ties",
         () ->
            top(3, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 0, 0, 5, 6, 5)
         ),
   
    test("should get top 4 a vector with entries with ties",
         () ->
            top(4, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 2, 0, 5, 6, 5)
         )

   ]);

# ------------------------------------------------------------------------------
# Function bottom
# ------------------------------------------------------------------------------

define bottom_suite = test_suite("Function bottom", [
    
    test("should do nothing for a zero vector",
         () ->
            bottom(3, test_vec(0, 0, 0, 0, 0, 0)) = test_vec(0, 0, 0, 0, 0, 0)
         ),
   
    test("should do nothing for a vector with one entry",
         () ->
            bottom(3, test_vec(0, 0, 1, 0, 0, 0)) = test_vec(0, 0, 1, 0, 0, 0)
         ),
   
    test("should be zero for a vector with one entry and bottom 0",
         () ->
            bottom(0, test_vec(0, 0, 1, 0, 0, 0)) = test_vec(0, 0, 0, 0, 0, 0)
         ),
   
    test("should get bottom 2 a vector with more entries",
         () ->
            bottom(2, test_vec(0, 2, 1, 4, 6, 5)) = test_vec(0, 2, 1, 0, 0, 0)
         ),
   
    test("should get bottom 2 a vector with entries with ties",
         () ->
            bottom(2, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 2, 1, 0, 0, 0)
         ),
   
    test("should get bottom 3 a vector with entries with ties",
         () ->
            bottom(3, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 2, 1, 5, 0, 0)
         ),
   
    test("should get bottom 4 a vector with entries with ties",
         () ->
            bottom(4, test_vec(0, 2, 1, 5, 6, 5)) = test_vec(0, 2, 1, 5, 0, 5)
         )

   ]);

# ------------------------------------------------------------------------------
# Function ranking
# ------------------------------------------------------------------------------

define ranking_suite = test_suite("Function ranking", [
    
    test("should do nothing for a zero vector",
         () ->
            ranking(test_vec(0, 0, 0, 0, 0, 0)) = test_vec(0, 0, 0, 0, 0, 0)
         ),
   
   test("should partially rank a parially filled vector",
         () ->
            ranking(test_vec(1, 0, 3, 0, 2, 0)) = test_vec(1, 0, 3, 0, 2, 0)
         ),
   
    test("should completely rank a sorted vector",
         () ->
            ranking(test_vec(2, 4, 5, 5, 6, 7)) = test_vec(1, 2, 3, 4, 5, 6)
         ),
   
    test("should rank an unsorted vector",
         () ->
            ranking(test_vec(7, 6, 5, 0, 4, 4)) = test_vec(5, 4, 3, 0, 1, 2)
         )
   ]);

# ------------------------------------------------------------------------------
# Function total
# ------------------------------------------------------------------------------

define total_suite = test_suite("Function total", [
    
    test("should be zero for the zero vector",
         () ->
            total(test_vec(0, 0, 0, 0, 0, 0)) = 0
         ),
         
   test("should be correct a vector with one non-zero entry",
         () ->
            total(test_vec(0, 0, 0, 0, 10, 0)) = 10
         ),
         
   test("should be correct for a vector with multiple positive entries",
         () ->
            total(test_vec(0, 4, 0, 7, 10, 0)) = 21
         ),
         
   test("should be correct for a vector with multiple positive and negative entries",
         () ->
            total(test_vec(3, 6, -4, -7, 10, 0)) = 8
         )
   ]);

# ------------------------------------------------------------------------------
# Function min
# ------------------------------------------------------------------------------

define min_suite = test_suite("Function min", [
    
    test("should give the vector for two identical vectors",
         () ->
            min(test_vec(0, 4, 0, 7, 10, 0), test_vec(0, 4, 0, 7, 10, 0)) = test_vec(0, 4, 0, 7, 10, 0)
         ),

    test("should do element wise min",
         () ->
            min(test_vec(0, 12, 0, 7, -10, 0), test_vec(0, 4, 2, -7, -7, 1)) = test_vec(0, 4, 0, -7, -10, 0)
         )
   ]);

# ------------------------------------------------------------------------------
# Function max
# ------------------------------------------------------------------------------

define max_suite = test_suite("Function max", [
    
    test("should give the vector for two identical vectors",
         () ->
            max(test_vec(0, 4, 0, 7, 10, 0), test_vec(0, 4, 0, 7, 10, 0)) = test_vec(0, 4, 0, 7, 10, 0)
         ),

    test("should do element wise max",
         () ->
            max(test_vec(0, 12, 0, 7, -10, 0), test_vec(0, 4, 2, -7, -7, 1)) = test_vec(0, 12, 2, 7, -7, 1)
         )
   ]);

# ------------------------------------------------------------------------------
# Function tan
# ------------------------------------------------------------------------------

define tan_suite = test_suite("Function tan", [
    
    test("should be correct for 0",
         () ->
            tan(0*|radian|) = 0
         ),

    test("should be correct for pi/2",
         () ->
            tan(0.5*pi*|radian|) > 10^10
         ),

      test("should be correct for pi",
         () ->
            approximates(tan(pi*|radian|), 0, 15*|decimals|)
         ),
      
      test("should be correct for -pi/4",
         () ->
            approximates(tan(-0.25*pi*|radian|), -1, 15*|decimals|)
         )
   ]);

# ------------------------------------------------------------------------------
# Function expt
# ------------------------------------------------------------------------------

define expt_suite = test_suite("Function expt", [
    
    test("should be correct for scalars",
         () ->
            2 ^ 3 = 8
         ),

    test("should be correct matrices without units",
         () ->
            let mat = test_vec(1,2,3,4,5,6) '*' test_vec(1,2,3,4,5,6)^T in
               mat ^ 3 = mat * mat * mat
            end
         ),

    test("should be correct for negative powers",
         () ->
            let mat = test_vec(1,2,3,4,5,6) '*' test_vec(1,2,3,4,5,6)^T in
               approximates(mat^-3 * mat^3,  mat^0, 15*|decimals|)
            end
         ),

    test("should be correct for non-integer powers",
         () ->
            approximates(expt(1.5, 1.5), 1.83711730709, 11*|decimals|)
         )              
   ]);

# ------------------------------------------------------------------------------
# Function mexpt
# ------------------------------------------------------------------------------

define mexpt_suite = test_suite("Function mexpt", [
    
    test("should be correct for scalars",
         () ->
            2 '^' 3 = 8
         ),

   test("should be correct matrices without units",
         () ->
            let mat = test_vec(1,2,3,4,5,6) '*' test_vec(1,2,3,4,5,6)^T in
               mat '^' 3 = mat '*' mat '*' mat
            end
         ),

   test("should be correct for negative powers",
         () ->
            let invertible_matrix = test_vec2(1,1) '*' test_vec2(2,1)^T + test_vec2(1,3) '*' test_vec2(5,2)^T in
               approximates(invertible_matrix'^'-3 '*' invertible_matrix'^'3, invertible_matrix'^'0, 9*|decimals|)
            end
         ),

    test("should be correct for matrices with units",
         () ->
            let
               vec = test_vec(1,2,3,4,5,6) * |Bar!unit|,
               mat = vec '*' vec^D
            in
               approximates(mat '^' 3, mat '*' mat '*' mat, 14*|decimals|)
            end
         )       
   ]);

# ------------------------------------------------------------------------------
# Function random
# ------------------------------------------------------------------------------

define random_suite = test_suite("Function random", [
    
    test("should do something",
         () ->
            let r = random() in
               r >= 0 and r <= 1
            end
         )
   ]);

# ------------------------------------------------------------------------------
# Function mapnz
# ------------------------------------------------------------------------------

define mapnz_suite = test_suite("Function mapnz", [
    
    test("should do something",
         () ->
            mapnz((x) -> x + 1, test_vec(0, 0, 1, 0, 3, 4)) = test_vec(0, 0, 2, 0, 4, 5)
         )
   ]);

# ------------------------------------------------------------------------------
# Function kronecker
# ------------------------------------------------------------------------------

define kronecker_suite = test_suite("Function kronecker", [
    
    test("should do something",
         () ->
            false
         )
   ]);
